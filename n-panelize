# Copy this file into /usr/share/zsh/site-functions/
# and add 'autoload n-panelize` to .zshrc
#
# This function somewhat reminds the panelize feature from Midnight Commander
# It allows browsing output of arbitrary command. Example usage:
# v-panelize ls /usr/local/bin
#
# Uses n-list

emulate -L zsh

zmodload zsh/curses

local IFS="
"

[ -f ~/.config/znt/n-list.conf ] && . ~/.config/znt/n-list.conf

local list
local selected

NLIST_REMEMBER_STATE=0

if [ -t 0 ]; then
    # Check if there is proper input
    if [ "$#" -lt 1 ]; then
        echo "Usage: n-panelize {command} [option|argument] ... or command | n-panelize"
        return 1
    fi

    list=( `"$@"` )
    # TODO: $? doesn't reach user
    [ "$?" -eq 127 ] && return $?
else
    # Check if can reattach to terminal
    if [ ! -c /dev/tty ]; then
        echo "No terminal available (no /dev/tty)"
        return 1
    fi

    list=( "${(@f)"$(<&0)"}" )
    exec </dev/tty
fi

n-list "${list[@]}"

if [ "$REPLY" -gt 0 ]; then
    selected="$reply[$REPLY]"
    echo "You have selected $selected"
fi

# vim: set filetype=zsh:
